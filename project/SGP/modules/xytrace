!
! XYGraph[simple] support stuff.
!
! $Id: xytrace,v 1.1 1993-12-10 01:24:29 caraher Exp $

define widget xytrace-right intmenu 'x'
	title 'XYGraph options'
	line
! want color here but don't know how
! want line width here but no such option in xygraphs
	entry 'Line representation' 'xyrep line' \
		(PDParam (icon_component, 'representation-style') = 'line')
	entry 'Point representation' 'xyrep point' \
		(PDParam (icon_component, 'representation-style') = 'point')
	entry 'Cross representation' 'xyrep cross' \
		(PDParam (icon_component, 'representation-style') = 'cross')
	entry 'X-mark representation' 'xyrep xmark' \
		(PDParam (icon_component, 'representation-style') = 'xmark')
endmenu

procedure xyrep rep string
	parameter #icon_component representation-style #rep
endprocedure

!
! The field selection procedure.
!
procedure xytrace-field field string
	parameter #icon_component y-field #field
endprocedure





!
! Zap this trace.
!
procedure xytrace-remove 
	local plats PDParam(icon_component,'platform')
!
! If we are the only platform we really want to just zorch the whole
! component.
!
	if (plats = icon_platform)
		local dmcmd quote(concat4('remove ', ourname, ' ', icon_component))
		dm #dmcmd
		return
	endif
!
! Nope, there are others.  Just clean this one out.
!
	local pos ListPosition(icon_platform, plats)
	local plats quote(RmElement(plats,pos))
	parameter #icon_component platform #plats
	local clist RmElement(PdParam(icon_component,'field-color'), pos)
	parameter #icon_component 'field-color' #clist
endprocedure




define widget xy-an-left intmenu 'x'
	title 'XY Trace options'
	line
	entry 'Remove this trace' 'xytrace-remove'
endmenu


!
! The color tweaker, called out of simple-color
!
procedure xytrace-color color string
	local plats PDParam(icon_component,'platform')
	local pos ListPosition(icon_platform, plats)
	local clist PDParam(icon_component,'field-color')
	local clist ReplElement(clist, pos, color)
	parameter #icon_component field-color #clist
endprocedure



!
! Adjust limits stuff.
!
procedure xytrace-adjust comp string
	popup xytrace-limits
endprocedure


define widget xytrace-limits form 'xytrace adjuster'
	popup xyta-popup
	noheader
!
! Title line.
!
	label title 'Adjust time series limits'
	button 'apply' 'Apply' command xyta-apply
	button 'dismiss' 'Dismiss' color red command 'popdown #ui$form'
	label comp 'comp' color gray95 width 1
	newline
!
! time period
!
	label l1 'Time period:'
	blank time 'funky' width 60 command xyta-apply
	label l2 's = seconds, m = minutes, h = hours, d = days'
	newline
!
! Scale mode.
!
	label l3 'Scale mode:'
	button mode 'weird' command 'xyta-ToggleMode' width 70
!	newline
!
! Manual values
!
	label l4 'Manual scale min:'
	blank min 'min' width 60 command xyta-apply
	label l5 'max:'
	blank max 'max' width 60 command xyta-apply
enddef


procedure xyta-popup
	local c icon_component
	local f PDParam(c,'y-field')
	formtext #ui$form comp #c
!
! Do the time period.
!
	local period PDParam('global','xy-series-span')
	formtext #ui$form time #period
!
! Fill in the scale mode.
!
	local mode PDSearch(c,'scale-y-mode','xy')
	if (mode = '(Undefined)')
		local mode 'autoscale'
	endif
	formtext #ui$form mode #mode
!
! And the manual values.
!
	local min PDSearch(c,'scale-y-min',f)
	if (min = "(Undefined)")
		local min "0.0"
	endif
	formtext #ui$form min #min
	local max PDSearch(c,'scale-y-max',f)
	if (max = "(Undefined)")
		local max "0.0"
	endif
	formtext #ui$form max #max
endprocedure


procedure xyta-apply
	local c getftext(ui$form,'comp')
	local t getftext(ui$form,'time')
	local f PDParam(c,'y-field')

	parameter 'global' 'xy-series-span' #t
	local mode getftext(ui$form,'mode')
	parameter #c 'scale-y-mode' #mode
	local min getftext(ui$form,'min')
	local param concat(f,'-scale-y-min')
	parameter 'global' #param #min
	local max getftext(ui$form,'max')
	local param concat(f,'-scale-y-max')
	parameter 'global' #param #max
endprocedure


!
! Tweak autoscale mode.
!
procedure xyta-ToggleMode
	local current getftext(ui$form, 'mode')
	if (current = 'manual')
		formtext #ui$form 'mode' 'autoscale'
	else
		formtext #ui$form 'mode' 'manual'
	endif
endprocedure
