!
! Add data to a siros-template window.  Just change the platform in all
! of the components to the given platform and field.  Based on rass-add.
!

procedure siros-replace platform string field string window string
!
! Actually check things?
!
	if (not substring (platform, 'siros'))
		message 'This is not a siros platform!'
		beep
		return
	endif
!
! OK, throw it in to all components in the window.
!
!	local comp NthComp(window, 1)
        local comps pd_complist(window)
        foreach comp #comps
           parameter #window #comp platform #platform
        endfor
endprocedure


!
! Build our siros component based upon the p_xytrace library component
!
pdlift p_xytrace p_xytrace p_siros
parameter p_siros p_siros annot-middle-menu siros-fields
parameter p_siros p_siros icon-middle-menu siros-fields
parameter p_siros p_siros dimensions 'channel=0'
parameter p_siros p_siros require "xytrace, simple-color, siros"


!
! Just like tseries add, but we use the siros pd component instead
!
procedure siros-add platform string field string window string
	local comps pd_complist(window)
	local nt 0
!
! Put the window on hold first.
!
	parameter #window global plot-hold true
!
! Go through the list of components in search of existing traces.  Count them.
!
	foreach comp #comps
		if (comp <> 'global' and comp <> 'xy-grid')
			if (nt = 0)
				local dcomps comp
			else
				local dcomps concat3(dcomps, ' ', comp)
			endif
			local nt (nt + nvalue(window, comp, 'platform'))
		endif
	endfor
!
! OK, if we found anything, pass through again and add the new platform
! to each.
!
	if (nt > 0)
		foreach comp #dcomps
			message 'comp #' comp
			local plats PDParam(window, comp, 'platform')
			local plats quote(concat3(plats, ',', platform))
			parameter #window #comp platform #plats
			local colors PDParam(window, comp, 'field-color')
			local fcolor NthColor('xygraph', nt)
			local colors quote(concat3(colors, ',', fcolor))
			parameter #window #comp field-color #colors
			local nt (nt + 1)
		endfor
	else
!
! there is no data component, we're starting from a blank
! window.
!
		add p_siros p_siros #window
		parameter #window p_siros platform #platform
		parameter #window p_siros y-field #field
		parameter #window p_siros axis-left true
		parameter #window p_siros axis-right false
		parameter #window p_siros axis-l-grid-intensity 0.7
		local fcolor NthColor('xygraph', 0)
		parameter #window p_siros field-color #fcolor
	endif
!
! Release the window and we are done.
!
	parameter #window global plot-hold false
endprocedure
