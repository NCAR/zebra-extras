
require simple-color
require xytrace

!
! The color tweaker for xy components with single fields,
! which also changes icon-color and the axis color
!
procedure xytrace-all-color color string
	local plats PDParam(icon_component,'platform')
	local pos ListPosition(icon_platform, plats)
	local clist PDParam(icon_component,'field-color')
	local clist ReplElement(clist, pos, color)
	parameter #icon_component field-color #clist
	parameter #icon_component icon-color #color
	if (PDParam(icon_component,"axis-left") = "true")
		parameter #icon_component "axis-l-color" #color
	endif
	if (PDParam(icon_component,"axis-right") = "true")
		parameter #icon_component "axis-r-color" #color
	endif
	if (PDParam(icon_component,"axis-top") = "true")
		parameter #icon_component "axis-t-color" #color
	endif
	if (PDParam(icon_component,"axis-bottom") = "true")
		parameter #icon_component "axis-b-color" #color
	endif
endprocedure

!
! Redefine the right menu to include the xmgr pipe option
!
define widget xytrace-right intmenu 'x'
	title 'XYGraph options'
	line
	entry 'Send to ACE/gr' 'xmgrpipe'
	line
	entry 'Line representation' 'xyrep line' \
		(PDParam (icon_component, 'representation-style') = 'line')
	entry 'Point representation' 'xyrep point' \
		(PDParam (icon_component, 'point-icon') = 'point')
	entry 'Cross representation' 'xyrep cross' \
		(PDParam (icon_component, 'point-icon') = 'cross')
	entry 'X-mark representation' 'xyrep xmark' \
		(PDParam (icon_component, 'point-icon') = 'xmark')
	entry 'Circle representation' 'xyrep fmill-loc' \
		(PDParam (icon_component, 'point-icon') = 'fmill-loc')
	line
	submenu 'Side annotation size' xysideannot
	entry 'Show time in side annotation' 'ToggleBoolean time-annot' \
		(PDParam (icon_component, 'time-annot') = 'true')
endmenu


procedure xmgrpipe
!
! Build a pipe command using the current platform, field, and times
!
	local cmd "xmgrpipe "
	local xfield PDParam(icon_component,"x-field")
	if (xfield <> "time")
		message "xmgrpipe only works for x-fields of time so far"
		return
	endif
	set cmd concat3(cmd,PDParam(icon_component,"xyprivate-data-begin")," ")
	set cmd concat3(cmd,PDParam(icon_component,"xyprivate-data-end")," ")
	set cmd concat3(cmd,PDParam(icon_component,"platform")," ")
	set cmd concat3(cmd,PDParam(icon_component,"y-field")," ")
	set cmd quote(concat3(cmd,"| xvgr -pipe &"))
	message "#" cmd
	shell #cmd

endprocedure

