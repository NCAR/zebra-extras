!
! Various widgets of interest.
!
!
! Stuff for window creation.
!
define widget cw_wintypes intmenu 'Coare version'
       title 'Window types'
       line
       entry 'Constant Altitude Plot' 'cw_type "Constant altitude"'
       entry 'Time-series' 'cw_type "Time series"'
       entry 'Skew-t' 'cw_type "Skew-t"'
;       entry 'Wind profile' 'cw_type "Wind profile"'
	entry 'Time-height profile' 'cw_type "Time-height profile"'
       entry 'RASS virtual temperature profile' 'cw_type "RASS profile"'
       entry 'Location Map' 'cw_type "Location Map"'
       entry 'Line Plot' 'cw_type "Line Plot"'
       entry 'Depth profile (CTD)' 'cw_type "Depth profile"'
       entry 'Time/depth CTD profile' 'cw_type "Time/depth profile"'
       entry 'Time/depth contour plot' 'cw_type "Time/depth contour"'
endmenu

procedure ProjectNewWindow type string
	if (type = 'Depth profile')
		set nw_pd 'depth-profile-template'
	elseif (type = 'Time/depth profile')
	        set nw_pd 'time-depth-template'
	elseif (type = 'Time/depth contour')
	       set nw_pd 'depth-contour-template'
	elseif (type = 'Wind profile')
	       set nw_pd 'wind-template'
	elseif (type = 'Time-height profile')
	       set nw_pd 'time-height-template'
	elseif (type = 'RASS profile')
	       set nw_pd 'rass-template'
	elseif (type = 'Location Map')
	       set nw_pd 'location-template'
	elseif (type = 'Line Plot')
	       set nw_pd 'lineplot-template'
	endif
	set nw_bmap 'xyplot'
endprocedure



!
! OK, here's stuff for dealing with time-series plots.
!

procedure TSNewField
	local w dm$button_window
	parameter #w global plot-hold true
!
! Kludge #1: assume that the third component in the PD is an xytrace.  If
!	     they don't do anything unusual, that will be the case.
!
	local sc NthComp(w, 2)
!
! Take the source component, and add it back to the window.  
!
	add #w #sc #w 0
!
! Kludge #2: we have no easy way to know what name the new component was 
!	     assigned.  It is certainly different from the source!  So we
!	     have to simply step through and see.
!
! We also count traces (in the older components only) while we are at it.
!
	local comps pd_complist(w)
	local ntrace 0
	local nlast 0
	foreach comp #comps
		local ntrace (ntrace + nlast)
		local new comp
		if (comp <> 'global' and comp <> 'xy-grid')
			local nlast NValue(w, comp, 'platform')
		endif
	endfor
!
! Time to assign a new set of colors for the new component.
!
	local p 0
	local nplat NValue(w, new, 'platform')
	while (p < nplat)
		local color NthColor('xygraph', ntrace+p)
		if (p = 0)
			local clist color
		else
			local clist concat3(clist, ',', color)
		endif
		local p (p + 1)
	endwhile
	local clist quote(clist)	! Some joker used "hot pink"
	parameter #w #new field-color #clist
!
! Kludge #3, pick a new field for this trace.  Someday we'll have a nice
!	     way for the user to do that; for now we just have to choose
!	     something and let them tweak it.
!
	local foundone false
	foreach field tdry rh dp pt ! that should do it
		if (not foundone)   ! wish we had "break"
			local hasit false
			foreach comp #comps
				if (PDParam (w, comp, 'y-field') = field)
					local hasit true
				endif
			endfor
			if (not hasit)
				local foundone true
				local newfield field
			endif
		endif
	endfor
!
! Store the new field, release the window, and we are done.  Also force
! the new axis on the right, since there's no room on the left in the
! tseries template.
!
	parameter #w #new y-field #newfield
	parameter #w #new axis-left false
	parameter #w #new axis-right true
	parameter #w global plot-hold false
endprocedure
