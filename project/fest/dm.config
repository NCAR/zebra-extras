!
! Display manager configuration file.
!
! $Id: dm.config,v 1.2 1994-12-12 20:49:01 burghart Exp $
!

!
! Set up path variables.
!
set CTablePath concat('colortables,',CTablePath)

!
! Load in our plot descriptions.  Load the library pd's first so that
! our project-specific template pd's take precedence.
!
message 'Loading:'
message '     ...plot descriptions...'
set libpd concat(c$libdir,'/pd')
pddir #libpd
pddir pd
! pddir dconfig/pd

!
! Note that $HOME/.zeb_configs is added to the configpath, but the
! dir to save configs to will always default to ./dconfig
!
set configdir 'dconfig'
set home getenv("HOME")
if (home <> "UNDEFINED")
	set configpath concat3(home,"/.zeb_configs,",configdir)
else
	set configpath #configdir
endif

!
! The graphics process initialization file.
!
set init_file concat(c$basedir, "/fest/gp.init")
!
! Make history time stay constant during config changes
!
set ForceHistory true
set initial_time getenv("INITIAL_TIME")
set default_time '25-Feb-92,12:00'
if (initial_time = "UNDEFINED") then
	history all #default_time
elseif (initial_time = "")
	history all #default_time
else
	history all #initial_time
endif
!
! Look for binaries we execute in ZEB_BINDIR, if it's been specified.
!
set binary_dir getenv("ZEB_BINDIR")
if (binary_dir <> "UNDEFINED") then
	set execpath binary_dir
endif
!
! Most of the interesting stuff is in other files -- if you looked here,
! you lose!
!
message '     ...button maps...'
read ButtonMaps
message '     ...display configurations ...'
read DisplayConfigs
!message '     ...color tables...'
!read ColorTables
message '     ...the icon bar...'
read IconBar


!
! Handle zooming out of window menus.
!
procedure zoom window string
	if (dm$config = "zoom")		! Already zoomed -- go back
		display #previous_config
	else
		set previous_config dm$config
		display zoom #window
	endif
endprocedure

!
! Reset a window to its default plot description.  Expects to be invoked
! from a graphics process.
!
procedure ResetWindow
	local pdname pd_param(dm$button_window,"global","pd-name","string")
	newpd dm$button_window pdname
endprocedure


!
! Stuff for the demo sounding stuff.  I'm not sure it's useful for anything,
! and should probably go away.
!
set IsSounding false

procedure toggle_snd
	if (IsSounding)
		display picture
	else
		display pictures
	endif
	set IsSounding (not IsSounding)
endprocedure

!
! Tweak some window system specific features.
!
if (getenv("OPENWINHOME") <> "UNDEFINED")
	set sleepafter	1
	set sleepfor	5
	set tbspace 25
endif


!
! Come up with an interesting display.
!

!set filepath concat(c$basedir, "/tmp")
!parameter defaults defaults file-path #filepath
parameter defaults defaults post-proc-mode true
set config getenv("DEFAULT_CONFIG")
if (config = "UNDEFINED")
	set config "fest"
endif
display #config

!!
!! allow fest specific choice of config windows
!!
!define widget pd-menu intmenu 'PD Templates'
!	title 'Select a Plot Description'
!	line
!	entry 'Search PD Directory' 'pddir dconfig/pd'
!	line
!	entry 'Midwest'	'formtext #ui$form pdname "one-UpperLeft"'
!	entry 'US' 'formtext #ui$form pdname "festdemo-UpperRight"' 
!	entry 'CP4' 'formtext #ui$form pdname "festdemo-LowerLeft"' 
!	entry 'Skew-t' 'formtext #ui$form pdname "festdemo-UpperLeft"'
!	entry 'Xsection' 'formtext #ui$form pdname "xsect-LowerLeft"'
!	entry 'Profiler' 'formtext #ui$form pdname "profiler-UpperLeft"' 
!	entry 'XYTimePlot' 'formtext #ui$form pdname "xytimegraph-UpperLeft"'
!	entry 'XYAltitudePlot' 'formtext #ui$form pdname "xygraph-UpperLeft"'
!endmenu
!

define widget cw_wintypes intmenu 'StormFEST Model CD'
	title 'FEST Window Templates'
	line
	entry 'Constant Altitude Model'	'cw_type "Constant Altitude Model"'
	entry 'Model X-Section'	'cw_type "Model X-Section"'
	entry 'Midwest'	'cw_type "Midwest"'
	entry 'US' 'cw_type "US"' 
	entry 'CP4' 'cw_type "CP4"' 
	entry 'StormFEST Skew-t' 'cw_type "StormFEST Skew-t"'
	entry 'Xsection' 'cw_type "Xsection"'
	entry 'Profiler' 'cw_type "Profiler"' 
	entry 'XYTimePlot' 'cw_type "XYTimePlot"'
	entry 'XYAltitudePlot' 'cw_type "XYAltitudePlot"'
endmenu



procedure ProjectNewWindow t string

	if (t = 'Constant Altitude Model')
		set nw_pd 'model-cap-template'
		set nw_bmap 'big'
	elseif (t = 'Model X-Section')
		set nw_pd 'model-xsect-template'
		set nw_bmap 'big'
	elseif (t = 'Midwest')
		set nw_pd 'midwest-template'
		set nw_bmap 'big'
	elseif (t = 'US')
		set nw_pd 'us-template'
		set nw_bmap 'big'
	elseif (t = 'CP4')
		set nw_pd 'cp4-template'
		set nw_bmap 'big'
	elseif (t = 'StormFEST Skew-t')
		set nw_pd 'skewt-template'
		set nw_bmap 'sounding'
	elseif (t = 'Xsection')
		set nw_pd 'fest-xsect-template'
		set nw_bmap 'big'
	elseif (t = 'Profiler')
		set nw_pd 'profiler-template'
		set nw_bmap 'big'
	elseif (t = 'XYTimePlot')
		set nw_pd 'tseries-template'
		set nw_bmap 'xygraph'
	elseif (t = 'XYAltitudePlot')
		set nw_pd 'xyaltitude-template'
		set nw_bmap 'xygraph'
	else
		set nw_pd 'cap-template'
		set nw_bmap 'big'
	endif

endprocedure
