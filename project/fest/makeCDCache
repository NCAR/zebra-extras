#!/bin/csh
#
# Generate a new set of cache files for a STORM-FEST CD.
#
# Files ./cache.iopX will be generated for the IOPs on the currently mounted 
# CD.  EventLogger information will be written to ./makeCDCache.log.
#
# Usage: makeCDCache <cd_directory>
#

if ($#argv != 1) then
	echo "Usage: $0 <cd_directory>"
	exit 1
endif

set startdir = $cwd

#
# Start up the zeb basics
#
setenv ZEB_SOCKET /tmp/zeb.makecache

message
timer &
EventLogger -n -f makeCDCache.log &

#
# For each IOP on the CD...
#
cd $1/data
foreach iop (iop*)
	echo -n Doing $iop...
	setenv DATA_DIR $1/data/$iop

	set cachefile = "$startdir/cache.$iop"

	#
	# Build a dsDaemon input file (ds.config + scan + write cache + exit)
	#
	cd /zeb/project/fest

	set dsfile = "/tmp/makeCDCache.ds.config"

	cat ds.config > $dsfile
	echo "set CacheOnExit false" >> $dsfile
	echo "rescan" >> $dsfile
	echo "cache $cachefile"	>> $dsfile
	echo "exit" >> $dsfile

	#
	# Run the dsDaemon
	#
	dsDaemon $dsfile
	rm -f $dsfile

	echo done

end

zebstop

