#!/bin/csh -f
# $Id: proj_startup,v 1.3 1996-02-06 22:22:55 corbet Exp $
#
# The fest-specific project startup file.  I expect to be sourced from 
# the "startzeb" script.
#
set cdpresent=0
#
# We need to try to figure out if they have a CD mounted.
#
if ( ! $?CDDIR ) setenv CDDIR .
#
# If a remote data dir is already set, we don't want to change it, so no
# sense looking for a CD
#
if ( ! $?REM_DATA_DIR ) then
	foreach dir ( $CDDIR /cd /cdrom /CDROM /zeb /cdrom/cdrom0 )
		if ( -f $dir/.cd_index) then
			set cdpresent=1
			set cddir=$dir
			setenv ZEB_CDROM $dir
			echo " "
			echo "CD-ROM found on" $dir
			break
		endif
	end
endif

if ( $?REM_DATA_DIR ) then
	echo "Using remote data directory: $REM_DATA_DIR"
	set iop=1
	set cddatadir=$REM_DATA_DIR
#
# If, in fact, there is a CD, let's get an IOP number from them.
#
else if ( $cdpresent ) then
#
# Figure out which disk we have
#
	if (-f $cddir/data/iop1/aster/stormfest.033.cdf) then
		set legal1="[1-7]"
		set legal2="<none>"
		set msg='This disk has observational data from IOPs 1-7'
		set modelcd=0
	else if (-f $cddir/data/iop8/aster/stormfest.049.cdf) then
		set legal1="[89]"
		set legal2="1[0-5]"
		set msg='This disk has observational data from IOPs 8-15'
		set modelcd=0
	else if (-f $cddir/data/iop16/aster/stormfest.062.cdf) then
		set legal1="1[6-9]"
		set legal2="20"
		set msg='This disk has observational data from IOPs 16-20'
		set modelcd=0
	else if (-f $cddir/data/iop3/avnold/avnold.1992.02.09.00.grib) then
		set legal1="[3-7]"
		set legal2="<none>"
		set msg='This disk has model data from IOPs 3-7'
		set modelcd=1
	else if (-f $cddir/data/iop10/avnold/avnold.1992.02.22.12.grib) then
		set legal1="1[0-2]"
		set legal2="1[6-9]"
		set msg='This disk has model data from IOPs 10-12 and 16-19'
		set modelcd=1
	else
		echo "...but it does not seem to be a STORM-FEST disk."
		echo "Ignoring the mounted CD."
		set cdpresent=0
		set iop=1
		set cddatadir=.
		goto finish
	endif
#
# Which IOP?
#
get_iop:
	echo $msg
	echo ' '
	echo -n 'Which IOP do you want to see (iop number or "none")? '
	set iop=$<
#
# Verify it.
#
	switch ($iop)
		case [nN]*:
			echo 'IOP data will not be available from the CD.'
			set cdpresent=0
			set iop=1
			set cddatadir=.
			goto finish
		case ${legal1}:
		case ${legal2}:
			breaksw
		default:
			echo ' '
			echo "IOP $iop is not on this disk"
			echo ' '
			goto get_iop
	endsw
#
# Set the data directory
#
	if ($iop == 6 || $iop == 7) then
	   	set iopname=6_7
	else if ($iop == 13 || $iop == 14) then
	     	set iopname=13_14
	else
		set iopname=$iop
	endif
	set cddatadir=$cddir/data/iop$iopname
#
# Figure out where the cache files will be.
#
	set systype = `uname`
	if ( $systype == Linux ) then
		set cachedir=lcache
	else
		set cachedir=cache
	endif
#
# Look for a cache file for observational data.
#
	if ( $modelcd && -f ${cachedir}/modelcache.iop$iopname ) then
		setenv REM_UNIFIED_CACHE ${cachedir}/modelcache.iop$iopname
		echo 'Remote data cache file is' $REM_UNIFIED_CACHE
	else if ( ! $modelcd && -f ${cachedir}/cache.iop$iopname ) then
		setenv REM_UNIFIED_CACHE ${cachedir}/cache.iop$iopname
		echo 'Remote data cache file is' $REM_UNIFIED_CACHE
	else
		echo 'No cache file'
	endif

	setenv REM_DATA_DIR $cddatadir
	setenv REMOTE_NAME CDROM	 # for dsdwidget
else
	echo 'No CDROM present.'
	set iop=1
	set cddatadir=.
endif

finish:

#
# Do some final tweaking and we're set.
#
if ( ! $?DEFAULT_CONFIG ) setenv DEFAULT_CONFIG model_xsect

#
# Warn the user about any impending creation of data directories
#
if ( ! -d $DATA_DIR/avnold ) then
	echo ' '
	echo "The DataStore daemon will be creating platform directories"
	echo "under $DATA_DIR when it starts up."
	echo "Please be patient, as this can take as long as a minute."
	echo ' '
endif

if (-f $DATA_DIR/defaulttime.iop$iop) then
	setenv INITIAL_TIME `cat $DATA_DIR/defaulttime.iop$iop`
else if ($cdpresent && -f $cddatadir/defaulttime) then
	setenv INITIAL_TIME `cat $cddatadir/defaulttime`
else
	setenv INITIAL_TIME 1-feb-92,00:05
endif

