#!/bin/csh
#
# Watch for new GOES files, move them to the appropriate Zebra data
# directories, and let the data store know about them
#
set echo
echo "$0 started at `date`"
set nonomatch
unalias cd	# GRRRRRRR

#
# we only want one of me; kill any others
#
onintr -	# ignore the INT signal we're about to send out...
killall -s INT $0:t
onintr		# pay attention to signals again

#
# loop forever
#

while (1)
    foreach plat (goes_1km goes_4km)
	set srcdir = /scr/data/${plat}_tmp
	set destdir = /scr/data/spol/$plat

	cd $srcdir
	set pattern = "*nc"
	set filelist = $pattern
	if ("$filelist" == "$pattern") continue
	
	foreach file ($filelist)
	    mv $file $destdir
	    dsrescan -file $file $plat
	end
    end
    sleep 30
end
