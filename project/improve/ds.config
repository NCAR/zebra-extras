!
! ds configuration for STEPS
!
set undef "UNDEFINED"

!
! The elev data dir must be absolute, else it will be appended to the
! source data directory by the daemon.  So we rely on proj_env to set
! the absolute path, else we fallback to the project directory
! (which is not always absolute).
!
set elevdir getenv("ELEV_DATA_DIR")
if (elevdir = undef)
	set elevdir concat(c$projdir,"/elev")
endif

!
! Our default source
!
source main
	directory #c$datadir
	PlatDir elev #elevdir
endsource

!
! Class definitions
!
read ds.classes

!
! Useful null platform
!
platform null
	organization	scalar
	filetype	netcdf
endplatform

!
! The radars
!
instance Sweepfile	spol particles precip.basic precip.hourly precip.storm

!
! Bistatic winds, in "ddop" form
!
instance NC3DGrid	binet_gridded

!
! topographic data
!
instance NCGrid		elev

!
! boundaries
!
instance BoundaryClass	boundary

!
! Manually-entered aircraft positions
!
instance NCMobileScalar convairmanual

! batch aircraft tracks from data file from the aircraft
instance NCMobileScalar convair

! Cleanup code.
!

set SECONDSPERHOUR 3600

procedure DoCleanup hours real
	truncate hours*SECONDSPERHOUR spol
	truncate hours*SECONDSPERHOUR particles
	truncate hours*SECONDSPERHOUR precip.basic
	truncate 3*hours*SECONDSPERHOUR precip.hourly
	truncate hours*SECONDSPERHOUR precip.storm
endprocedure

set minfree 1000000

procedure CleanupScan	
	set hours 24
	while (FreeSpace (c$datadir) < minfree and hours > 3)
		DoCleanup hours
		set hours (hours - 1)
	endwhile
endprocedure

if (getenv("DS_DISABLE_CLEANUP") = undef)
	message "Datastore: enabling periodic data cleaning!"
	every 600 'CleanupScan'
else
	message "Datastore: data cleaning has been disabled."
endif

