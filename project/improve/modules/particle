!
! Particle-specific stuff, dependent on spol
!

require spol


define widget particle-middle intmenu 'x'
	title 'Particle discrimination (and related)'
	line
	submenu 'Fields' 'part-fields'
	submenu 'Representation' 'spol-rep'
	submenu 'Color tables' 'Color-tables'
	line
	entry 'Hold angle mode' 'ToggleBoolean every-sweep' \
		(PDParam (icon_component, "every-sweep") = "false")
endmenu



define widget part-fields intmenu 'x'
	title 'Particle discrimination (and related)'
	line
	entry 'NCAR Particle types' 'partfield pd' \
		(PDParam (icon_component, 'field') = 'pd')
	entry 'UW Particle types' 'partfield wpd' \
		(PDParam (icon_component, 'field') = 'wpd')
	entry 'Hail detection ratio' 'partfield hdr' \
		(PDParam (icon_component, 'field') = 'hdr')
	entry 'NKDP' 'partfield nkdp' \
		(PDParam (icon_component, 'field') = 'nkdp')
endmenu



!
! Switch fields, and handle the details of color tables too
!
procedure partfield field string 
	local f PDParam(icon_component, 'field')
	if (f = field)
		return
	endif
	! Only save the current table if it's not a particle table
	local ctable PDParam(icon_component, 'color-table')
	local svtable PDParam(icon_component,'saved-ctable')
	if (svtable = '(Undefined)')
		local svtable 'wild'
	endif
	if (ctable = 'particle')
	elseif (ctable = 'uwparticle')
	else
		local svtable ctable
	endif
	if (field = 'pd')
		parameter #icon_component field 'pd'
		parameter #icon_component saved-ctable #svtable
		parameter #icon_component color-table 'particle'
	elseif (field = 'wpd')
		parameter #icon_component field 'wpd'
		parameter #icon_component saved-ctable #svtable
		parameter #icon_component color-table 'uwparticle'
	else
		if (f = 'pd')
			parameter #icon_component color-table #svtable
		elseif (f = 'wpd')
			parameter #icon_component color-table #svtable
		endif
		parameter #icon_component field #field
	endif
endprocedure

